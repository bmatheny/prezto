#
# Executes commands at the start of an interactive session.
#
# Authors:
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#


#
# Path management unfortunately needs to be done here, and first (before we
# load prezto), because it can't be specified in zshenv which is where it
# probably belongs. This is because zprofile loads after zshenv, and the system
# wide zprofile may set its own path variables. At does on my work machine at
# least.
#
path=("/usr/local/bin" $path)

# Create a robust platform name string so that if homebrew is installed in a
# location using this name, it will work on that machine.
function get_platform_name() {
  local os_type=$(uname -s) # Darwin, Linux
  local machine_type=$(uname -m) # x86_64, arm64, aarch64
  local flavor="" # rocky, ubuntu, 12.6 (for mac)
  if [[ -s "/etc/os-release" ]]; then
    source "/etc/os-release"
    if [ -n "${ID}" ]; then
      flavor="${ID}_${VERSION_ID:-Unknown}"
    fi
    if [[ "$OSTYPE" == darwin* ]]; then
      flavor="$(sw_vers -productVersion)"
    fi
  fi
  if [ -z "${flavor}" ]; then
    flavor="unknown"
  fi
  echo "${os_type}-${machine_type}-${flavor}" # e.g. Linux-x86_64-rocky
}

# Update path variable to include brew location, if found
function update_homebrew_path() {
  # This is most likely set by `brew shellenv` and we don't want to set it again
  if [[ -n "${HOMEBREW_CELLAR}" ]]; then
    return
  fi

  # This is set by this script and if it was already set we don't need to set it again
  if [[ -n "${HOMEBREW_PLATFORM_NAME}" ]]; then
    return
  fi

  # Prefer local bin file since it doesn't polluate variable namespace
  if [ -x "$HOME/bin/platform_name" ]; then
    typeset -l HOMEBREW_PLATFORM_NAME=$($HOME/bin/platform_name)
  else
    typeset -l HOMEBREW_PLATFORM_NAME=$(get_platform_name)
  fi

  local homebrew_locations=(
    "${HOME}/.homebrew/${HOMEBREW_PLATFORM_NAME}"
    "${HOME}/homebrew/${HOMEBREW_PLATFORM_NAME}"
    "/usr/local")
  for location in $homebrew_locations; do
    if [[ -d "$location/bin" && -x "$location/bin/brew" ]]; then
      path=("$location/bin" "$location/sbin" $path)
      break
    fi
  done
}
# Load this before zprezto init so that homebrew module gets loaded correctly
update_homebrew_path

#
# Load prezto
#
if [[ -s "${ZDOTDIR:-$HOME}/.zprezto/init.zsh" ]]; then
  source "${ZDOTDIR:-$HOME}/.zprezto/init.zsh"
fi

if (( $+commands[brew] )); then
  if [ $(brew --prefix coreutils 2>/dev/null) ]; then
    path=("$(brew --prefix coreutils)/libexec/gnubin" $path)
  fi
  if [ $(brew --prefix ruby 2>/dev/null) ]; then
    path=("$(brew --prefix ruby)/bin" $path)
    path=("$(gem environment gemdir)/bin" $path)
  fi
fi

#
# Personal customizations, load zshplugin files
#
if [[ -d  "${HOME}/.config/zsh/plugins" ]]; then
  for config_file ($HOME/.config/zsh/plugins/*.zsh) source $config_file
fi
